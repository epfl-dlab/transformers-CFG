root ::= (smiles bond?)* ( group_symbol_left group_bond? | group_radical_left bond? )  smiles+ | smiles+ ( bond? group_radical_right | group_bond? group_symbol_right) (bond? smiles)*

group_radical_left ::=  "(" ( group_symbol_left group_bond? smiles* )+ ")"

group_radical_right ::= "(" ( smiles* group_bond? group_symbol_right )+ ")"

group_bond ::= ( "-" | "\\" | "/" )

group_symbol_left ::= "OC" | "N"

group_symbol_right ::= "CO"

smiles ::= atom ( chain | branch )*

chain ::= (dot atom | bond? ( atom | ring_closure ) )+

branch ::= "(" ( ( dot | bond )? smiles )+ ")"


atom ::= organic_symbol | aromatic_symbol | atom_spec | wildcard | group_symbol_left

bond ::= "-" | "=" | "#" | "$" | ":" | "/" | "\\"

dot ::= "."

atom_spec ::= "[" isotope? ( "se" | "as" | aromatic_symbol | element_symbol | wildcard ) chiral_class? h_count? ( charge? | class? ) "]"

organic_symbol ::= "Br" | "Cl" | "N" | "O" | "P" | "S" | "F" | "I" | "B" | "C"

aromatic_symbol ::= "b" | "c" | "n" | "o" | "p" | "s"

wildcard ::= "*"

element_symbol  ::= "A" ( "c" | "g" | "l" | "m" | "r" | "s" | "t" | "u" ) |
                    "B" ( "a" | "e" | "h" | "i" | "k" | "r" )? |
                    "C" ( "a" | "d" | "e" | "f" | "l" | "m" | "n" | "o" | "r" | "s" | "u" )? |
                    "D" ( "b" | "s" | "y" ) |
                    "E" ( "r" | "s" | "u" ) |
                    "F" ( "e" | "l" | "m" | "r" )? |
                    "G" ( "a" | "d" | "e" ) |
                    "H" ( "e" | "f" | "g" | "o" | "s" )? |
                    "I" ( "n" | "r" )? |
                    "K" "r"? |
                    "L" ( "a" | "i" | "r" | "u" | "v" ) |
                    "M" ( "c" | "g" | "n" | "o" | "t" ) |
                    "N" ( "a" | "b" | "d" | "e" | "h" | "i" | "o" | "p" )? |
                    "O" ( "g" | "s" )? |
                    "P" ( "a" | "b" | "d" | "m" | "o" | "r" | "t" | "u" )? |
                    "R" ( "a" | "b" | "e" | "f" | "g" | "h" | "n" | "u" ) |
                    "S" ( "b" | "c" | "e" | "g" | "i" | "m" | "n" | "r" )? |
                    "T" ( "a" | "b" | "c" | "e" | "h" | "i" | "l" | "m" | "s" ) |
                    "U" | "V" | "W" | "Xe" | "Y" "b"? |
                    "Z" ( "n" | "r" )


ring_closure ::= "%" [1-9] [0-9] | [0-9]

chiral_class ::= ( "@" ( "@" | "TH" [1-2] | "AL" [1-2] | "SP" [1-3] | "TB" ( "1" [0-9]? | "2" "0"? | [3-9] ) | "OH" ( "1" [0-9]? | "2" [0-9]? | "3" "0"? | [4-9] ) )? )?

charge   ::= "-" ( "-" | "0" | "1" [0-5]? | [2-9] )? | "+" ( "+" | "0" | "1" [0-5]? | [2-9] )?

h_count ::= "H" [0-9]?

class ::= ":" [0-9]+

isotope ::= [1-9] [0-9]? [0-9]?
